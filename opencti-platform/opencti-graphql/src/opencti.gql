define

  ## PROPERTIES

  # migration properties
  uuid sub attribute, datatype string;
  title sub attribute, datatype string;
  lastRun sub attribute, datatype string;
  timestamp sub attribute, datatype long;

  # OpenCTI properties
  internal_id_key sub attribute, datatype string;
  created_at sub attribute, datatype date;
  created_at_day sub attribute, datatype string;
  created_at_month sub attribute, datatype string;
  created_at_year sub attribute, datatype string;
  updated_at sub attribute, datatype date;
  issuer sub attribute, datatype string;
  duration sub attribute, datatype string;
  password sub attribute, datatype string;
  email sub attribute, datatype string;
  firstname sub attribute, datatype string;
  lastname sub attribute, datatype string;
  language sub attribute, datatype string;
  grant sub attribute, datatype string;
  platform_title sub attribute, datatype string;
  platform_language sub attribute, datatype string;
  platform_email sub attribute, datatype string;
  platform_url sub attribute, datatype string;
  platform_external_auth sub attribute, datatype boolean;
  platform_registration sub attribute, datatype boolean;
  platform_demo sub attribute, datatype boolean;
  graph_data sub attribute, datatype string;
  workspace_data sub attribute, datatype string;
  workspace_type sub attribute, datatype string;
  object_status sub attribute, datatype long;
  score sub attribute, datatype long;
  tag_type sub attribute, datatype string;

  # STIX common properties
  stix_id_key sub attribute, datatype string;
  stix_label sub attribute, datatype string;
  entity_type sub attribute, datatype string;
  created sub attribute, datatype date;
  modified sub attribute, datatype date;
  revoked sub attribute, datatype boolean;
  first_seen sub attribute, datatype date;
  first_seen_day sub attribute, datatype string;
  first_seen_month sub attribute, datatype string;
  first_seen_year sub attribute, datatype string;
  last_seen sub attribute, datatype date;
  last_seen_day sub attribute, datatype string;
  last_seen_month sub attribute, datatype string;
  last_seen_year sub attribute, datatype string;
  expiration sub attribute, datatype date;
  expiration_day sub attribute, datatype string;
  expiration_month sub attribute, datatype string;
  expiration_year sub attribute, datatype string;
  name sub attribute, datatype string;
  description sub attribute, datatype string;
  connector_type sub attribute, datatype string;
  connector_scope sub attribute, datatype string;

  # STIX SDO
  alias sub attribute, datatype string;
  stix_role sub attribute, datatype string;
  objective sub attribute, datatype string;
  goal sub attribute, datatype string;
  sophistication sub attribute, datatype string;
  resource_level sub attribute, datatype string;
  primary_motivation sub attribute, datatype string;
  secondary_motivation sub attribute, datatype string;
  personal_motivation sub attribute, datatype string;
  tool_version sub attribute, datatype string;
  kill_chain_name sub attribute, datatype string;
  phase_name sub attribute, datatype string;
  phase_order sub attribute, datatype long;
  definition_type sub attribute, datatype string;
  definition sub attribute, datatype string;
  level sub attribute, datatype long;
  color sub attribute, datatype string;
  source_name sub attribute, datatype string;
  url sub attribute, datatype string;
  hash sub attribute, datatype string;
  external_id sub attribute, datatype string;
  published sub attribute, datatype date;
  published_day sub attribute, datatype string;
  published_month sub attribute, datatype string;
  published_year sub attribute, datatype string;
  organization_class sub attribute, datatype string;
  report_class sub attribute, datatype string;
  pattern sub attribute, datatype string;
  valid_from sub attribute, datatype date;
  valid_until sub attribute, datatype date;
  platform sub attribute, datatype string;
  required_permission sub attribute, datatype string;
  source_confidence_level sub attribute, datatype long;

  # STIX SRO
  relationship_type sub attribute, datatype string;
  role_played sub attribute, datatype string;
  infrastructure sub attribute, datatype boolean;
  weight sub attribute, datatype long;

  # STIX SOO
  number sub attribute, datatype long;
  rir sub attribute, datatype string;
  observable_value sub attribute, datatype string;
  value sub attribute, datatype string;
  hash_type sub attribute, datatype string;

  ## ROLES

  # Migration
  status sub role;
  state sub role;

  # OpenCTI roles
  author sub role;
  authorization sub role;
  client sub role;
  allow sub role;
  allowed sub role;
  grouping sub role;
  member sub role;
  tagging sub role;

  # STIX common roles
  so sub role;
  soo sub role;
  kill_chain_phase sub role;

  # STIX embeded roles
  creator sub role;
  marking sub role;
  external_reference sub role;
  phase_belonging sub role;
  knowledge_aggregation sub role;

  # STIX observables embeded roles
  resolves_to sub role;
  belongs_to sub role;

  # STIX observables roles
  observable_group sub role;
  observable_part_of sub role;

  # STIX SRO roles

  # targets
  target sub role;
  source sub role;

  # uses
  usage sub role;
  user sub role;

  # attributed-to
  origin sub role;
  attribution sub role;

  # mitigates
  problem sub role;
  mitigation sub role;

  # indicates
  characterize sub role;
  indicator sub role;

  # variant-of
  original sub role;
  variation sub role;

  # impersonates
  genuine sub role;
  dummy sub role;

  # related-to
  relate_to sub role;
  relate_from sub role;

  # localization
  location sub role;
  localized sub role;

  # belonging
  gather sub role;
  part_of sub role;

  # drops
  dropping sub role;
  dropped sub role;

  # sighting
  sighting_of sub role;
  sighted_in sub role;

  ## RELATIONSHIPS

  # Login relationship
  authorize sub relation,
    key internal_id_key,
    relates authorization,
    relates client;

  # Migration relationships
  migrate sub relation,
    key internal_id_key,
    relates status,
    relates state;

  # Group membership relationships
  membership sub relation,
    key internal_id_key,
    relates grouping,
    relates member;

  # Access relationships
  permission sub relation,
    key internal_id_key,
    has grant,
    relates allow,
    relates allowed;

  user_permission sub relation,
    key internal_id_key,
    has grant,
    relates allow,
    relates allowed;

  # OpenCTI embeded relationships
  relation_embedded sub relation,
    abstract,
    key internal_id_key;

  authored_by sub relation_embedded,
    relates author,
    relates so;

  owned_by sub relation_embedded,
    relates owner,
    relates to;

  tagged sub relation_embedded,
    relates tagging,
    relates so;

  # STIX embeded relationships
  stix_relation_embedded sub relation,
    abstract,
    key internal_id_key;

  created_by_ref sub stix_relation_embedded,
    relates creator,
    relates so;

  object_marking_refs sub stix_relation_embedded,
    relates marking,
    relates so;

  external_references sub stix_relation_embedded,
    relates external_reference,
    relates so;

  kill_chain_phases sub stix_relation_embedded,
    relates phase_belonging,
    relates kill_chain_phase;

  object_refs sub stix_relation_embedded,
    relates so,
    relates knowledge_aggregation;

  # STIX SOO embeded relationships
   stix_relation_observable_embedded sub relation,
    abstract,
    key internal_id_key,
    has created,
    has modified,
    has revoked,
    has entity_type,
    has relationship_type,
    has weight,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    has name,
    has description,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays so;

  resolves_to_refs sub stix_relation_observable_embedded,
    relates soo,
    relates resolves_to;

  belongs_to_refs sub stix_relation_observable_embedded,
    relates soo,
    relates belongs_to;

  # STIX SOO embeded relationships
   stix_relation_observable_grouping sub relation,
    abstract,
    key internal_id_key,
    has created,
    has modified,
    has revoked,
    has entity_type,
    has relationship_type,
    has weight,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    has name,
    has description,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    relates observable_group,
    relates observable_part_of;

  # STIX SRO relationships
  stix_sighting sub relation,
    key internal_id_key,
    key stix_id_key,
    has created,
    has modified,
    has revoked,
    has entity_type,
    has relationship_type,
    has weight,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    relates sighted_in,
    relates sighting_of;

  stix_relation sub relation,
    abstract,
    key internal_id_key,
    key stix_id_key,
    has created,
    has modified,
    has revoked,
    has entity_type,
    has relationship_type,
    has weight,
    has score,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    has expiration,
    has expiration_day,
    has expiration_month,
    has expiration_year,
    has name,
    has description,
    has role_played,
    has infrastructure,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays phase_belonging,
    plays so;

  # targets
  targets sub stix_relation,
    plays localized,
    relates target,
    relates source;

  # uses
  uses sub stix_relation,
    relates usage,
    relates user;

  # related-to
  related-to sub stix_relation,
    relates relate_from,
    relates relate_to;

  # mitigates
  mitigates sub stix_relation,
    relates problem,
    relates mitigation;

  # impersonates
  impersonates sub stix_relation,
    relates genuine,
    relates dummy;

  # indicates
  indicates sub stix_relation,
    relates characterize,
    relates indicator;

  # comes-after
  comes-after sub stix_relation,
    relates coming_from,
    relates coming_after;

  # parentship
  parentship sub stix_relation,
    abstract;

  # attributed-to
  attributed-to sub parentship,
    relates origin,
    relates attribution;

  # variant-of
  variant-of sub parentship,
    relates original,
    relates variation;

  # localization
  localization sub parentship,
    relates location,
    relates localized;

  # belonging
  gathering sub parentship,
    relates gather,
    relates part_of;

  # drops
  drops sub parentship,
    relates dropping,
    relates dropped;

  ## ENTITIES
  # Migration entities
  MigrationStatus sub entity,
    key internal_id_key,
    has lastRun,
    plays status;

  MigrationReference sub entity,
    key internal_id_key,
    has title,
    has timestamp,
    plays state;

  # OpenCTI entities
  Settings sub entity,
    key internal_id_key,
    has entity_type,
    has platform_title,
    has platform_email,
    has platform_url,
    has platform_language,
    has platform_external_auth,
    has platform_registration,
    has platform_demo,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at;

  Tag sub entity,
    key internal_id_key,
    has tag_type,
    key value,
    has color,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays tagging;

  Connector sub entity,
    key internal_id_key,
    has name,
    has connector_type,
    has connector_scope,
    has created_at,
    has updated_at;

  Group sub entity,
    key internal_id_key,
    has entity_type,
    has name,
    has description,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays allowed,
    plays grouping;

  Workspace sub entity,
    key internal_id_key,
    has entity_type,
    has workspace_type,
    has name,
    has description,
    has graph_data,
    has workspace_data,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays to,
    plays so,
    plays knowledge_aggregation;

  Token sub entity,
    key internal_id_key,
    key uuid,
    has entity_type,
    has name,
    has revoked,
    has created,
    has duration,
    has issuer,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays authorization;

  Stix-Domain sub entity,
    abstract,
    key internal_id_key,
    key stix_id_key,
    has stix_label,
    has entity_type,
    has created,
    has modified,
    has revoked,
    has object_status,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays relate_from,
    plays relate_to,
    plays so;

  Stix-Domain-Entity sub Stix-Domain,
    abstract,
    has name,
    has description,
    has alias,
    has graph_data;

  External-Reference sub Stix-Domain,
    has source_name,
    has description,
    has url,
    has hash,
    has external_id,
    plays external_reference;

  Kill-Chain-Phase sub Stix-Domain,
    has kill_chain_name,
    has phase_name,
    has phase_order,
    plays kill_chain_phase;

  Marking-Definition sub Stix-Domain,
    has definition_type,
    has definition,
    has level,
    has color,
    plays marking,
    plays allow;

  Indicator sub Stix-Domain,
    has pattern,
    has valid_from,
    has valid_until,
    plays phase_belonging,
    plays indicator;

  Attack-Pattern sub Stix-Domain-Entity,
    has platform,
    has required_permission,
    plays phase_belonging,
    plays user,
    plays usage,
    plays problem,
    plays characterize,
    plays coming_from,
    plays coming_after;

  Campaign sub Stix-Domain-Entity,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    has objective,
    plays attribution,
    plays origin,
    plays user,
    plays source,
    plays characterize,
    plays phase_belonging;

  Course-Of-Action sub Stix-Domain-Entity,
    plays mitigation;

  Identity sub Stix-Domain-Entity,
    abstract,
    plays gather,
    plays part_of,
    plays creator,
    plays target,
    plays origin,
    plays attribution,
    plays dummy,
    plays user,
    plays genuine,
    plays localized;

  Region sub Identity,
    plays location;

  Country sub Identity,
    plays localized,
    plays location;

  City sub Identity,
    plays localized,
    plays location;

  Sector sub Identity;

  Organization sub Identity,
    has organization_class;

  Threat-Actor sub Identity,
    has stix_role,
    has goal,
    has sophistication,
    has resource_level,
    has primary_motivation,
    has secondary_motivation,
    has personal_motivation,
    plays attribution,
    plays source,
    plays user,
    plays characterize,
    plays localized;

  User sub Identity,
    has password,
    has email,
    has firstname,
    has lastname,
    has language,
    has grant, # roles is a reserved word
    plays client,
    plays author,
    plays owner,
    plays member,
    plays allowed;

  Incident sub Stix-Domain-Entity,
    has objective,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    plays attribution,
    plays user,
    plays source,
    plays characterize,
    plays phase_belonging;

  Intrusion-Set sub Stix-Domain-Entity,
    has first_seen,
    has first_seen_day,
    has first_seen_month,
    has first_seen_year,
    has last_seen,
    has last_seen_day,
    has last_seen_month,
    has last_seen_year,
    has goal,
    has sophistication,
    has resource_level,
    has primary_motivation,
    has secondary_motivation,
    plays attribution,
    plays source,
    plays user,
    plays origin,
    plays characterize,
    plays phase_belonging;

  Malware sub Stix-Domain-Entity,
    plays phase_belonging,
    plays source,
    plays user,
    plays variation,
    plays usage,
    plays problem,
    plays characterize,
    plays attribution,
    plats dropping,
    plays dropped,
    plays original;

  Report sub Stix-Domain-Entity,
    has report_class,
    has published,
    has published_day,
    has published_year,
    has published_month,
    has source_confidence_level,
    plays knowledge_aggregation,
    plays phase_belonging;

  Tool sub Stix-Domain-Entity,
    has tool_version,
    plays phase_belonging,
    plays source,
    plays user,
    plays usage,
    plays problem,
    plays dropping,
    plays dropped,
    plays characterize;

  Vulnerability sub Stix-Domain-Entity,
    plays target,
    plays problem;

   Stix-Observable sub entity,
    abstract,
    key internal_id_key,
    key stix_id_key,
    has entity_type,
    has name,
    has description,
    key observable_value,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays so,
    plays soo,
    plays indicator,
    plays observable_group,
    plays observable_part_of;

    Domain sub Stix-Observable;

    IPv4-Addr sub Stix-Observable,
    plays localized,
    plays resolves_to;

    IPv6-Addr sub Stix-Observable,
    plays localized,
    plays resolves_to;

    URL sub Stix-Observable;

    Email sub Stix-Observable;
    Email-Address sub Email;
    Email-Subject sub Email;

    Mutex sub Stix-Observable;

    File sub Stix-Observable;
    File-Name sub File;
    File-Path sub File;
    File-MD5 sub File;
    File-SHA1 sub File;
    File-SHA256 sub File;

    Registry-Key sub Stix-Observable;
    Registry-Key-Value sub Registry-Key;

    PDB-Path sub Stix-Observable;

    Windows-Service sub Stix-Observable;
    Windows-Service-Name sub Windows-Service;
    Windows-Service-Display-Name sub Windows-Service;

    Windows-Scheduled-Task sub Stix-Observable;

    X509-Certificate sub Stix-Observable;
    X509-Certificate-Issuer sub X509-Certificate;
    X509-Certificate-Serial-Number sub X509-Certificate;

    Stix-Observable-Data sub entity,
    abstract,
    key internal_id_key,
    has entity_type,
    has value,
    has created_at,
    has created_at_day,
    has created_at_month,
    has created_at_year,
    has updated_at,
    plays so;

    Autonomous-System sub Stix-Observable-Data,
    has number,
    has name,
    has rir,
    plays belongs_to;.

    ## ACCESS RULES
    #PermissionRule sub rule
    #when {
    #  (member: $user, grouping: $group) isa membership;
    #  (allowed: $group, allow: $marking) isa permission;
    #  $marking isa Marking-Definition has level $markingLevel has definition_type $markingType;
    #  $markingDefinition isa Marking-Definition has level $markingDefinitionLevel has definition_type $markingDefinitionType;
    #  $markingDefinitionType == $markingType;
    #  $markingDefinitionLevel <= $markingLevel;
    #} then {
    #  (allowed: $user, allow: $markingDefinition) isa user_permission;
    #};

    ## USES RULES
    AttributionUsesRule sub rule,
    when {
      (origin: $origin, attribution: $entity) isa attributed-to;
      (user: $entity, usage: $object) isa uses;
    }, then {
      (user: $origin, usage: $object) isa uses;
    };

    ## INDICATES RULES
    AttributionIndicatesRule sub rule,
    when {
      (origin: $origin, attribution: $entity) isa attributed-to;
      (characterize: $entity, indicator: $object) isa indicates;
    }, then {
      (characterize: $origin, indicator: $object) isa indicates;
    };

    UsageIndicatesRule sub rule,
    when {
      (user: $origin, usage: $entity) isa uses;
      (characterize: $entity, indicator: $object) isa indicates;
    }, then {
      (characterize: $origin, indicator: $object) isa indicates;
    };

    ## TARGETS RULES
    AttributionTargetsRule sub rule,
    when {
      (origin: $origin, attribution: $entity) isa attributed-to;
      (source: $entity, target: $target) isa targets;
    }, then {
      (source: $origin, target: $target) isa targets;
    };
    
    GatheringTargetsRule sub rule,
    when {
      (gather: $parent, part_of: $entity) isa gathering;
      (source: $source, target: $entity) isa targets;
    }, then {
      (source: $source, target: $parent) isa targets;
    };

    LocalizationTargetsRule sub rule,
    when {
      (location: $location, localized: $target) isa localization;
      (source: $entity, target: $target) isa targets;
    }, then {
      (source: $entity, target: $location) isa targets;
    };

    LocalizationOfTargetsRule sub rule,
    when {
      $rel(source: $entity, target: $target) isa targets;
      (location: $location, localized: $rel) isa localization;
    }, then {
      (source: $entity, target: $location) isa targets;
    };

    ## ATTRIBUTED-TO RULES
    AttributionAttributionRule sub rule,
    when {
      (origin: $origin, attribution: $entity) isa attributed-to;
      (origin: $entity, attribution: $subentity) isa attributed-to;
    }, then {
      (origin: $origin, attribution: $subentity) isa attributed-to;
    };

    ## LOCALIZATION RULES
    LocalizationLocalizationRule sub rule,
    when {
      (location: $location, localized: $entity) isa localization;
      (location: $entity, localized: $subentity) isa localization;
    }, then {
      (location: $location, localized: $subentity) isa localization;
    };